/*
 * This macro creates 2-channel stack of nuclei and cell-body for segmentation using cellpose. 
 * It is designed to work after extracting IMC files using the 'pre-processing' step of 'ImcSegmentationPipeline' by the Bodenmiller lab 
 * (https://bodenmillergroup.github.io/ImcSegmentationPipeline/prepro.html). 
 * The cell-body image is created by normalising non-nuclei channels (values range between 0 - (2^16-1) = 65535) which were set to '1' 
 * in the ilastik column of "panel.csv".
 * Inputs: 1. Image stacks in 'ilastik' folder. 2. "panel.csv".
 * Outputs: 1. 2-channel stack of nuclei and cell-body. 2. Random cropped area of each image for training in cellpose. 
 * Note: You need to set variables and directories in the first section below before running code. 
 * 
 * Known bugs: I have changed the file separator for directories to "/". It used to be 'File.separator' which is supposed to work across systems but wasn't.
 * 
 * Author: heeva.baharlou@gmail.com (24/05/2023)
 */
 
roiManager("reset");
run("Clear Results");
run("Close All");

setBatchMode(false);





// **** CHANGE: Set variables and directories 
// Set your 'analysis' folder directory
dir = "C:/Users/oscar/Desktop/IMC/LizAnalysisTest/analysis";
// Set your 'panel.csv' file directory
panelDir = "C:/Users/oscar/Desktop/IMC/LizAnalysisTest/raw/panel.csv";
// Change this to what you call your nuclei stain under the 'Target' column in 'panel.csv'. This usually corresponds to the Ir191 or Ir193 Metal tags. 
dna = "DNA";





//NO NEED TO CHANGE anything from here
dirImages = dir + "/" + "for_segmentation";
imOutput = dir +  "/" + "cellpose";

//Size (in pixels) of the square for cropping
square_size = 200;
//Create the cellpose if doesn't exist. This will contain the 2-channel stack of the whole image generated by this script. Crops will have a randomly croped area for training. 
if(File.exists(imOutput) == 0) File.makeDirectory(imOutput);

//****


//Get list of images in the directory.
list = getFileList(dirImages);
a = newArray();
index = 0;
for (i=0; i<list.length; i++) {
    if (endsWith(list[i], ".tiff") || endsWith(list[i], ".png") || endsWith(list[i], ".tif")) {
        a[index] = dirImages + "/" + list[i];
        index = index + 1;
    }
}

list = a;

//Make an array of 'Targets' in panel.csv that were selected for segmentation ("1" in the 'Segment' column)
open(panelDir);

IJ.renameResults("panel.csv", "Results");
vMarkers = newArray();
index = 0;
var dnaIndex;
for (i=0; i<nResults; i++) {
    ilastikValue = getResult("Segment", i);
    if (ilastikValue == 1) {
        a = getResultString("Target",i);
        vMarkers[index] = a;
        //save position of the nuclei stain in the array. Use later to select this channel. 
        if(a == dna) dnaIndex = index;
        index = index + 1;
    }
}

// Go through each image and add to the manager if not to be excluded
for (i=0; i<list.length; i++) {
    
    open(list[i]);
    imTitle = getTitle();
    imTitle = substring(imTitle, 0, lengthOf(imTitle)-5);
    
	// Normalise all images from stack so values span 0 - (2^16 -1)
	stackTitle = getTitle();
	run("Split Channels");
	imageList = getList("image.titles");
	for (j = 0; j < lengthOf(imageList); j++) {
	    selectWindow(imageList[j]);
	    run("Enhance Contrast...", "saturated=0.10 normalize");
	    //rename images by channel number so you can select the DNA channel out later. 
	    rename(toString(j));
	    }
	
	//re-stack images excluding nuclei channel

	selectWindow(dnaIndex);
	//add slice so that the dna channel doesn't get stacked
	run("Add Slice");
	run("Images to Stack", "use keep");
	run("Z Project...", "projection=[Average Intensity]");
	body = getTitle();
	
	//add duplicate channel - For OSCAR
	selectWindow(dnaIndex);
	run("Duplicate...", "duplicate");
	rename("empty");
	run("Delete Slice");
	Stack.getDimensions(width, height, channels, slices, frames);
	for(x=0; x<width; x++) {
		for(y=0;y<height;y++){
			SetPixel(x,y,0);
		}
	}

	//remove slice from dna channel and make stack with projected image (cell body).
	selectWindow(dnaIndex);
	run("Delete Slice");
	run("Merge Channels...", "c1=duplicate c2=AVG_Stack c3=" + dnaIndex + " create");
	run("Stack to Hyperstack...", "order=xyczt(default) channels=2 slices=1 frames=1 display=Composite");
	
	//adjust contrast
	Stack.setDisplayMode("color");
	Stack.setChannel(1);
	run("Blue");
	run("Clear Results");
	run("Set Measurements...", "area mean min redirect=None decimal=3");
	run("Measure");
	setMinAndMax(0, getResult("Max", 0)/2);
	Stack.setChannel(2);
	run("Clear Results");
	run("Measure");
	setMinAndMax(0, getResult("Max", 0)/2);
	Stack.setDisplayMode("composite");
	
	//name image and save
	rename(imTitle + "_CpSeg");
	saveAs("tiff", imOutput + "/" + getTitle());
	
	roiManager("reset");
	run("Clear Results");
	run("Close All");
	
}
